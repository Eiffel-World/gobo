indexing

	description:

		"Routines that ought to be in class DOUBLE"

	library: "Gobo Eiffel Kernel Library"
	copyright: "Copyright (c) 2003, Eric Bezault and others"
	license: "Eiffel Forum License v2 (see forum.txt)"
	date: "$Date$"
	revision: "$Revision$"

class KL_DOUBLE_ROUTINES

#ifdef ISE
inherit
	ANY

	DOUBLE_MATH
		rename
			log as old_log,
			log10 as old_log10,
			exp as old_exp
		export
			{NONE} all
		end

#endif
#ifdef VE
inherit
	ANY

	MATH
		rename
			log as old_log,
			exp as old_exp
		export
			{NONE} all
		end

#endif
feature -- Logarithms

	log (d: DOUBLE): DOUBLE is
			-- Natural logarithm of `d'
		require
			d_positive: d > 0.0
		do
#ifdef SE
			Result := d.log
#endif
#ifdef ISE
			Result := old_log (d)
#endif
#ifdef HACT
			Result := double_math.log (d)
#endif
#ifdef VE
			Result := old_log (d.truncated_to_real)
#endif
		end

	log2 (d: DOUBLE): DOUBLE is
			-- Base 2 logarithm of `d'
		require
			d_positive: d > 0.0
#ifdef VE || SE
		local
#endif
#ifdef SE
			a: DOUBLE
#endif
#ifdef VE
			a: REAL
#endif
		do
#ifdef ISE
			Result := log_2 (d)
#endif
#ifdef VE || SE
			a := 2.0
#endif
#ifdef SE
			Result := d.log / a.log
#endif
#ifdef VE
			Result := old_log (d.truncated_to_real) / log (a)
#endif
		end

	log10 (d: DOUBLE): DOUBLE is
			-- Base 10 logarithm of `d'
		require
			d_positive: d > 0.0
		do
#ifdef SE
			Result := d.log10
#endif
#ifdef ISE
			Result := old_log10 (d)
#endif
#ifdef HACT
			Result := double_math.log10 (d)
#endif
#ifdef VE
			Result := log (d) / log (10.0)
#endif
		end

feature -- Exponent

	exp (d: DOUBLE): DOUBLE is
			-- Inverse of the natural logarithm
		do
#ifdef SE
			Result := d.exp
#endif
#ifdef ISE
			Result := old_exp (d)
#endif
#ifdef VE
			Result := old_exp (d.truncated_to_real)
#endif
		end

	nth_root (d, n: DOUBLE): DOUBLE is
			-- `n'-th root of `d'
		require
#ifdef VE
			divisible: (1.0).divisible (n.truncated_to_real)
#else
			divisible: (1.0).divisible (n)
#endif
		do
#ifdef ISE || VE
			Result := d ^ (1.0 / n)
#endif
#ifdef SE
			Result := d.pow (1.0 / n)
#endif
		end

#ifdef HACT
feature {NONE} -- Implementation

	double_math: DOUBLE_MATH is
			-- Double math functions
			-- (Note: HACT 4.0.1 does not handle correctly the renaming
			-- of external features with no alias clause.)
		once
			create Result
		ensure
			double_math_not_void: Result /= Void
		end

#endif
end
