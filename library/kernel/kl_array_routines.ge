indexing

	description:

		"Routines that ought to be in class ARRAY"

	library:    "Gobo Eiffel Kernel Library"
	author:     "Eric Bezault <ericb@gobo.demon.co.uk>"
	copyright:  "Copyright (c) 1997, Eric Bezault"
	date:       "$Date$"
	revision:   "$Revision$"

class KL_ARRAY_ROUTINES [G]

feature -- Initialization

	make_from_array (an_array: ARRAY [G]; min_index: INTEGER): ARRAY [G] is
			-- Create a new array and initialize it
			-- with items from `an_array'.
		require
			an_array_not_void: an_array /= Void
		local
			i, j, nb: INTEGER
		do
			!! Result.make (min_index, min_index + an_array.count - 1)
			from
				nb := an_array.upper
				i := an_array.lower
				j := min_index
			until
				i > nb
			loop
				Result.put (an_array.item (i), j)
				i := i + 1
				j := j + 1
			end
		ensure
			array_not_void: Result /= Void
			lower_set: Result.lower = min_index
			count_set: Result.count = an_array.count
--			same_items: forall i in Result.lower .. Result.upper,
--				Result.item (i) = an_array.item (i + an_array.lower - min_index)
		end

feature -- Status report

	has (an_array: ARRAY [G]; v: G): BOOLEAN is
			-- Does `v' appear in `an_array'?
		require
			an_array_not_void: an_array /= Void
#ifndef ISE || HACT
		local
			i, nb: INTEGER
		do
			from
				i := an_array.lower
				nb := an_array.upper
			until
				Result or i > nb
			loop
				Result := an_array.item (i) = v
				i := i + 1
			end
#else
		do
			Result := an_array.has (v)
#endif
		end

feature -- Element change

	subcopy (an_array, other: ARRAY [G]; start_pos, end_pos, index_pos: INTEGER) is
			-- Copy items of `other' within bounds `start_pos' and `end_pos'
			-- to `an_array' starting at index `index_pos'.
		require
			an_array_not_void: an_array /= Void
			other_not_void: other /= Void
			not_same: an_array /= other
			valid_start_pos: other.valid_index (start_pos)
			valid_end_pos: other.valid_index (end_pos)
			valid_bounds: start_pos <= end_pos + 1
			valid_index_pos: an_array.valid_index (index_pos)
			enough_space: (an_array.upper - index_pos) >= (end_pos - start_pos)
#ifndef ISE || HACT
		local
			i, j: INTEGER
		do
			from
				i := start_pos
				j := index_pos
			until
				i > end_pos
			loop
				an_array.put (other.item (i), j)
				j := j + 1
				i := i + 1
			end
#else
		do
			an_array.subcopy (other, start_pos, end_pos, index_pos)
#endif
		ensure
			-- copied: forall i in 0 .. (end_pos - start_pos),
			--     an_array.item (index_pos + i) = other.item (start_pos + i)
		end

feature -- Removal

	clear_all (an_array: ARRAY [G]) is
			-- Reset all items to default values.
		require
			an_array_not_void: an_array /= Void
#ifndef ISE || HACT || TOWER
		local
			i, nb: INTEGER
			v: G
		do
			from
				i := an_array.lower
				nb := an_array.upper
			until
				i > nb
			loop
				an_array.put (v, i)
				i := i + 1
			end
#else
		do
			an_array.clear_all
#endif
		ensure
			-- all_cleared: forall i in an_array.lower .. an_array.upper,
			--		an_array.item (i) = Void or else
			--		an_array.item (i) = an_array.item (i).default
		end

end -- class KL_ARRAY_ROUTINES
