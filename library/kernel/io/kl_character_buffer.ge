indexing

	description:

		"Character buffers"

	library: "Gobo Eiffel Kernel Library"
	copyright: "Copyright (c) 2001-2004, Eric Bezault and others"
	license: "MIT License"
	date: "$Date$"
	revision: "$Revision$"

class KL_CHARACTER_BUFFER

inherit

	KI_CHARACTER_BUFFER
		redefine
#ifdef ISE
			as_special,
#endif
			append_substring_to_string,
			fill_from_string, fill_from_stream,
			move_left, move_right
		end

#ifdef ISE
	STRING_HANDLER
		export {NONE} all end

	KL_SHARED_OPERATING_SYSTEM
		export {NONE} all end

#endif
	KL_IMPORTED_ANY_ROUTINES

create

	make, make_from_string

feature {NONE} -- Initialization

	make (n: INTEGER) is
			-- Create a new character buffer being able
			-- to contain `n' characters.
		do
#ifdef ISE
			if not operating_system.is_dotnet then
				create area.make (n + 1)
				area.set_count (n + 1)
				as_special := area.area
			else
				create area.make (n)
				area.set_count (n)
			end
#endif
#ifdef SE
			create area_string.make_filled ('%U', n + 2)
			area_string.remove_tail (1)
			area := area.from_pointer (area_string.to_external)
			count := n
#endif
		end

feature -- Access

	item (i: INTEGER): CHARACTER is
			-- Item at position `i'
		do
#ifdef ISE
			if as_special /= Void then
				Result := as_special.item (i)
			else
				Result := area.item (i)
			end
#else
			Result := area.item (i)
#endif
		end

	substring (s, e: INTEGER): STRING is
			-- New string made up of characters held in
			-- buffer between indexes `s' and `e'
#ifdef SE
		local
			i, nb: INTEGER
#endif
		do
			if e < s then
					-- Empty string
				create Result.make (0)
			else
#ifdef ISE
				if as_special /= Void then
					Result := area.substring (s + 1, e + 1)
				else
					Result := area.substring (s, e)
				end
#endif
#ifdef SE
				nb := e - s + 1
				create Result.make (nb)
				from i := s until i > e loop
					Result.append_character (area.item (i))
					i := i + 1
				end
#endif
			end
		end

feature -- Measurement

#ifdef SE
	count: INTEGER
			-- Number of characters in buffer
#else
	count: INTEGER is
			-- Number of characters in buffer
		do
#ifdef ISE
			if as_special /= Void then
				Result := area.count - 1
			else
				Result := area.count
			end
#else
			Result := area.count
#endif
		end
#endif

#ifdef ISE
feature -- Conversion

	as_special: SPECIAL [CHARACTER]
			-- 'SPECIAL [CHARACTER]' version of current character buffer;
			-- Characters are indexed starting at 1;
			-- May return void in some descendants, and the result may share
			-- the internal data with `Current'

#endif
feature -- Element change

	put (v: CHARACTER; i: INTEGER) is
			-- Replace character at position `i' by `v'.
		do
#ifdef ISE
			if as_special /= Void then
				as_special.put (v, i)
			else
				area.put (v, i)
			end
#else
			area.put (v, i)
#endif
		end

	append_substring_to_string (s, e: INTEGER; a_string: STRING) is
			-- Append string made up of characters held in buffer
			-- between indexes `s' and `e' to `a_string'.
#ifdef ISE
		local
			i, nb: INTEGER
			old_count, new_count: INTEGER
		do
			if s <= e then
				nb := e - s + 1
				old_count := a_string.count
				new_count := old_count + nb
				if new_count > a_string.capacity then
					a_string.resize (new_count)
				end
				if ANY_.same_types (a_string, dummy_string) then
					a_string.set_count (new_count)
					if as_special /= Void then
						a_string.subcopy (area, s + 1, e + 1, old_count + 1)
					else
						a_string.subcopy (area, s, e, old_count + 1)
					end
				else
					if as_special /= Void then
						from i := s until i > e loop
							a_string.append_character (as_special.item (i))
							i := i + 1
						end
					else
						from i := s until i > e loop
							a_string.append_character (area.item (i))
							i := i + 1
						end
					end
				end
			end
#endif
#ifdef SE
		local
			i, nb: INTEGER
			old_count, new_count: INTEGER
		do
			if s <= e then
				nb := e - s + 1
				old_count := a_string.count
				new_count := old_count + nb
				if new_count > a_string.capacity then
					a_string.resize (new_count)
					a_string.keep_head (old_count)
				end
				from i := s until i > e loop
					a_string.append_character (area.item (i))
					i := i + 1
				end
			end
#endif
		end

	fill_from_string (a_string: STRING; pos: INTEGER) is
			-- Copy characters of `a_string' to buffer
			-- starting at position `pos'.
		local
			nb: INTEGER
#ifndef ISE
			i, j: INTEGER
#endif
		do
			nb := a_string.count
			if nb > 0 then
#ifdef ISE
				if as_special /= Void then
					area.subcopy (a_string, 1, nb, pos + 1)
				else
					area.subcopy (a_string, 1, nb, pos)
				end
#else
				j := pos
				from i := 1 until i > nb loop
					area.put (a_string.item (i), j)
					j := j + 1
					i := i + 1
				end
#endif
			end
		end

	fill_from_stream (a_stream: KI_CHARACTER_INPUT_STREAM; pos, nb: INTEGER): INTEGER is
			-- Fill buffer, starting at position `pos', with
			-- at most `nb' characters read from `a_stream'.
			-- Return the number of characters actually read.
		do
#ifdef SE
			Result := a_stream.read_to_string (area_string, pos + 1, nb)
#else
#ifdef ISE
			if as_special /= Void then
				Result := a_stream.read_to_string (area, pos + 1, nb)
			else
				Result := a_stream.read_to_string (area, pos, nb)
			end
#else
			Result := a_stream.read_to_string (area, pos, nb)
#endif
#endif
		end

	move_left (old_pos, new_pos: INTEGER; nb: INTEGER) is
			-- Copy `nb' characters from `old_pos' to
			-- `new_pos' in buffer.
#ifndef ISE
		local
			i, j, nb2: INTEGER
#endif
		do
			if nb > 0 then
#ifdef ISE
				if as_special /= Void then
					area.subcopy (area, old_pos + 1, old_pos + nb, new_pos + 1)
				else
					area.subcopy (area, old_pos, old_pos + nb - 1, new_pos)
				end
#else
				j := new_pos
				nb2 := old_pos + nb - 1
				from i := old_pos until i > nb2 loop
					area.put (area.item (i), j)
					j := j + 1
					i := i + 1
				end
#endif
			end
		end

	move_right (old_pos, new_pos: INTEGER; nb: INTEGER) is
			-- Copy `nb' characters from `old_pos' to
			-- `new_pos' in buffer.
#ifndef ISE
		local
			i, j: INTEGER
#endif
		do
			if nb > 0 then
#ifdef ISE
				if as_special /= Void then
					area.subcopy (area, old_pos + 1, old_pos + nb, new_pos + 1)
				else
					area.subcopy (area, old_pos, old_pos + nb - 1, new_pos)
				end
#else
				j := new_pos + nb - 1
				from i := old_pos + nb - 1 until i < old_pos loop
					area.put (area.item (i), j)
					j := j - 1
					i := i - 1
				end
#endif
			end
		end

feature -- Resizing

	resize (n: INTEGER) is
			-- Resize buffer so that it contains `n' characters.
			-- Do not lose any previously entered characters.
#ifdef SE
		do
			area_string.resize (n + 2)
			area_string.remove_tail (1)
			area := area.from_pointer (area_string.to_external)
			count := n
#endif
#ifdef ISE
		do
			if as_special /= Void then
				area.resize (n + 1)
				area.set_count (n + 1)
				as_special := area.area
			else
				area.resize (n)
				area.set_count (n)
			end
#endif
		end

feature {NONE} -- Implementation

#ifdef ISE
	area: STRING
			-- Implementation
#else
#ifdef SE
	area: NATIVE_ARRAY [CHARACTER]
	area_string: STRING
			-- Implementation
#else
	area: STRING
			-- Implementation
#endif
#endif

invariant

#ifdef ISE
	area_not_void: area /= Void
#endif
#ifdef SE
	area_string_not_void: area_string /= Void
#endif

end
