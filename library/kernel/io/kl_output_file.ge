indexing

	description:

		"Output files"

	library:    "Gobo Eiffel Kernel Library"
	author:     "Eric Bezault <ericb@gobosoft.com>"
	copyright:  "Copyright (c) 2001, Eric Bezault and others"
	license:    "Eiffel Forum Freeware License v1 (see forum.txt)"
	date:       "$Date$"
	revision:   "$Revision$"

deferred class KL_OUTPUT_FILE

inherit

	KI_OUTPUT_FILE
#ifdef VE
		undefine
			copy,
				-- Make sure that this deferred class
				-- has a deferred routine.
			put_boolean
		end
#endif

	KL_FILE
		rename
			open as open_write,
			is_open as is_open_write
		end

#ifdef SE
	TEXT_FILE_WRITE
		rename
			make as old_make,
			is_connected as old_is_open_write,
			put_character as old_put_character,
			put_string as old_put_string,
			put_integer as old_put_integer,
			put_boolean as old_put_boolean,
			put_new_line as old_put_new_line,
			disconnect as old_close,
			flush as old_flush
		export
			{NONE} all
		end

#endif
feature -- Status report

	is_open_write: BOOLEAN is
			-- Is file opened in write mode?
		do
			Result := old_is_open_write
		end

feature -- Output

	put_character (c: CHARACTER) is
			-- Write `c' to output file.
		do
			old_put_character (c)
		end

	put_string (a_string: STRING) is
			-- Write `a_string' to output file.
		do
			old_put_string (a_string)
		end

feature -- Basic operations

	open_write is
			-- Open current file in write-only mode if
			-- it can be opened, let it closed otherwise.
			-- If the file is successfully opened, it is
			-- either created if it didn't exist or its
			-- old content is removed otherwise.
		local
			rescued: BOOLEAN
		do
			if not rescued then
				if name.count > 0 then
					old_open_write
				end
			elseif not is_closed then
				close
			end
		rescue
			if not rescued then
				rescued := True
				retry
			end
		end

	open_append is
			-- Open current file in append mode if it
			-- can be opened, let it closed otherwise.
			-- If the file is successfully opened, it is
			-- either created if it didn't exist or the
			-- data which will be written to the file will
			-- appear after its old content otherwise.
		local
			rescued: BOOLEAN
		do
			if not rescued then
				if name.count > 0 then
					old_open_append
				end
			elseif not is_closed then
				close
			end
		rescue
			if not rescued then
				rescued := True
				retry
			end
		end

	recursive_open_write is
			-- Open current file in write-only mode if
			-- it can be opened, let it closed otherwise.
			-- If the file is successfully opened, it is
			-- either created if it didn't exist or its
			-- old content is removed otherwise. Try to
			-- recursively create its parent directory
			-- if it does not exist yet.
		local
			a_dirname: STRING
			a_pathname: STRING
			a_dir: KL_DIRECTORY
		do
			open_write
			if not is_open_write then
				a_pathname := file_system.canonical_pathname (name)
				a_dirname := file_system.dirname (a_pathname)
				!! a_dir.make (a_dirname)
				if not a_dir.exists then
					a_dir.recursive_create_directory
					if a_dir.exists then
						open_write
					end
				end
			end
		end

	recursive_open_append is
			-- Open current file in append mode if it
			-- can be opened, let it closed otherwise.
			-- If the file is successfully opened, it is
			-- either created if it didn't exist or the
			-- data which will be written to the file will
			-- appear after its old content otherwise.
			-- Try to recursively create its parent directory
			-- if it does not exist yet.
		local
			a_dirname: STRING
			a_pathname: STRING
			a_dir: KL_DIRECTORY
		do
			open_append
			if not is_open_write then
				a_pathname := file_system.canonical_pathname (name)
				a_dirname := file_system.dirname (a_pathname)
				!! a_dir.make (a_dirname)
				if not a_dir.exists then
					a_dir.recursive_create_directory
					if a_dir.exists then
						open_append
					end
				end
			end
		end

	flush is
			-- Flush buffered data to disk.
		do
#ifndef VE
			old_flush
#endif
		end

#ifdef SE
feature {NONE} -- Implementation

	old_open_write is
			-- Open file in write mode.
		require
			name_not_void: name /= Void
			name_not_empty: name.count > 0
		deferred
		end

	old_open_append is
			-- Open file in append mode.
		require
			name_not_void: name /= Void
			name_not_empty: name.count > 0
		deferred
		end

#endif
end -- class KL_OUTPUT_FILE
