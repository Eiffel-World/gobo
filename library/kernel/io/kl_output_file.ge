indexing

	description:

		"Text output files. The character '%N' is automatically %
		%converted to the line separtor of the underlying %
		%file system when written to the file."

	library:    "Gobo Eiffel Kernel Library"
	author:     "Eric Bezault <ericb@gobosoft.com>"
	copyright:  "Copyright (c) 2001, Eric Bezault and others"
	license:    "Eiffel Forum Freeware License v1 (see forum.txt)"
	date:       "$Date$"
	revision:   "$Revision$"

class KL_OUTPUT_FILE

inherit

	KI_OUTPUT_FILE
		undefine
#ifndef VE
			put_new_line,
#endif
			put_integer
#ifdef VE
		redefine
			put_boolean, put_new_line
#endif
		end

	KL_OPERATING_SYSTEM
#ifdef SE
		rename
			rename_file as se_rename_file
#endif
		export
			{NONE} all
		end

#ifdef ISE
	PLAIN_TEXT_FILE
		rename
			make as ise_make,
			open_write as ise_open_write,
			close as do_close,
			delete as do_delete,
			put_boolean as ise_put_boolean
		export
			{NONE} all
		end
#endif
#ifdef HACT
	PLAIN_TEXT_FILE
		rename
			make as hact_make,
			open_write as hact_open_write,
			close as do_close,
			delete as do_delete,
			put_boolean as hact_put_boolean
		export
			{NONE} all
		end
#endif
#ifdef SE
	STD_FILE_WRITE
		rename
			make as se_make,
			rename_file as se_rename_file,
			is_connected as is_open_write,
			disconnect as do_close,
			put_boolean as se_put_boolean
		export
			{NONE} all
		end

	FILE_TOOLS
		rename
			rename_file as se_rename_file,
			delete as se_delete,
			tmp_string as ft_tmp_string
		export
			{NONE} all
		end
#endif
#ifdef VE
	FILE
		rename
			make as ve_make,
			open_write as ve_open_write,
			put_character as ve_put_character,
			put_string as ve_put_string,
			close as do_close,
			delete as do_delete,
			put_boolean as ve_put_boolean
		export
			{NONE} all
		end
#endif

creation

	make

feature {NONE} -- Initialization

	make (a_name: like name) is
			-- Create a new output file named `a_name'.
		do
#ifdef ISE || HACT
			name := a_name
			mode := Closed_file
#endif
#ifdef SE
			name := a_name
			se_make
#endif
#ifdef VE
			ve_make (a_name)
#endif
		end

feature -- Access

	eol: STRING is "%N"
			-- Line separator

#ifdef SE
	name: STRING
			-- File name

feature -- Status report

	is_closed: BOOLEAN is
			-- Is file closed?
		do
			Result := not is_open_write
		end

#endif
#ifdef VE
feature -- Output

	put_character (c: CHARACTER) is
			-- Write `c' to output file.
		do
			if is_windows and then c = '%N' then
					-- Windows.
				ve_put_character ('%R')
			end
			ve_put_character (c)
		end

	put_string (a_string: STRING) is
			-- Write `a_string' to output file.
		local
			i, nb: INTEGER
			c: CHARACTER
		do
			if is_windows then
					-- Windows.
				nb := a_string.count
				from i := 1 until i > nb loop
					c := a_string.item (i)
					if c = '%N' then
						ve_put_character ('%R')
					end
					ve_put_character (c)
					i := i + 1
				end
			else
				ve_put_string (a_string)
			end
		end

	put_boolean (b: BOOLEAN) is
			-- Write "True" to standard output file
			-- if `b' is true, "False" otherwise.
		do
			if b then
				ve_put_string (True_constant)
			else
				ve_put_string (False_constant)
			end
		end

	put_new_line is
			-- Write a new line to output file.
		do
			put_character ('%N')
		end

#endif
feature -- Basic operations

	open_write is
			-- Open current file in write-only mode if
			-- it can be opened, let it closed otherwise.
		local
			rescued: BOOLEAN
		do
			if not rescued then
#ifdef ISE
				ise_open_write
#endif
#ifdef HACT
				hact_open_write
#endif
#ifdef SE
				connect_to (name)
#endif
#ifdef VE
				ve_open_write
#endif
			elseif not is_closed then
				close
			end
		rescue
			if not rescued then
				rescued := True
				retry
			end
		end

#ifdef VE
	flush is
			-- Flush buffered data to disk.
		do
		end

#endif
	close is
			-- Close current file if it is closable,
			-- let it open otherwise.
		local
			rescued: BOOLEAN
		do
			if not rescued then
				do_close
			end
		rescue
			if not rescued then
				rescued := True
				retry
			end
		end

	rename_file (new_name: STRING) is
			-- Rename current file as `new_name'.
			-- Do nothing if the file could not be renamed
			-- or if it did not exist. Overwrite file
			-- `new_name' if it existed. If renaming was
			-- successful, then `name' is set to `new_name'.
		local
			rescued: BOOLEAN
			old_name: STRING
		do
			if not rescued then
				old_name := name
#ifdef SE
				if file_exists (name) then
					rename_to (name, new_name)
					if file_exists (new_name) then
						name := new_name
					end
				end
#endif
#ifdef ISE || HACT || VE
				if exists then
					change_name (new_name)
				end
#endif
			else
				name := old_name
			end
		rescue
			if not rescued then
				rescued := True
				retry
			end
		end

	delete is
			-- Delete current file.
			-- Do nothing if the file could not be
			-- deleted or if it did not exist.
		local
			rescued: BOOLEAN
		do
			if not rescued then
#ifdef SE
				se_delete (name)
#endif
#ifdef ISE || HACT || VE
				if exists then
					do_delete
				end
#endif
			end
		rescue
			if not rescued then
				rescued := True
				retry
			end
		end

end -- class KL_OUTPUT_FILE
